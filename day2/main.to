const input = read("input.txt")
const ranges = stringSplit(input, ",")

make c = 0
make c2 = 0

do {
    const start, end #= stringSplit(r, "-")
    
    do {
        set c += !is_valid(i) * i
        set c2 += !is_valid2(i) * i
    } for i in range(stringToInt(start), stringToInt(end) + 1)
} for r in ranges

do sayln(`Part 1: {c}`)
do sayln(`Part 2: {c2}`)

define is_valid (num) {
    const digits = len((string)num)
    if digits % 2 != 0 then {
        return true
    }

    return num % (10**(digits / 2) + 1) != 0
}

define is_valid2 (num) {
    const digits = len((string)num)
    if digits <= 1 then {
        return true
    }

    match digits => {
        2 => {
            return num % 11 != 0
        }
        3 => {
            return num % 111 != 0
        }
        4 => {
            return num % 101 != 0
        }
        5 => {
            return num % 11111 != 0
        }
        6 => {
            return num % 1001 != 0 && num % 10101 != 0 && num % 111111 != 0
        }
        7 => {
            return num % 1111111 != 0
        }
        8 => {
            return num % 10001 != 0
        }
        9 => {
            return num % 1001001 != 0
        }
        10 => {
            return num % 100001 != 0 && num % 101010101 != 0 && num % 1111111111 != 0
        }
        other => {
            do sayln(`Warning: unhandled number of digits {digits}`) when digits > 10
            return true
        }
    }

    return true
}