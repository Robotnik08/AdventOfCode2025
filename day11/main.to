const input = read("input.txt")
const lines = stringSplit(input, "\n")

const nodes = {}
do {
    const pair = stringSplit(l, ": ")
    set nodes->(pair#0) = stringSplit(pair#1, " ")
} for l in lines

define count_paths (start_node, end_node, memo) {
    return memo->(start_node) when memo->(start_node) !== null

    return 1 when start_node == end_node
    return 0 when start_node == "out"

    make total = 0
    set total += count_paths(child, end_node, memo) for child in nodes->(start_node)

    set memo->(start_node) = total
    return total
}

do sayln(`Part 1: {count_paths("you", "out", {})}`)
do sayln(`Part 2: {
    count_paths("svr", "dac", {}) * count_paths("dac", "fft", {}) * count_paths("fft", "out", {})
    + count_paths("svr", "fft", {}) * count_paths("fft", "dac", {}) * count_paths("dac", "out", {})
}`)