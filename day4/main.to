const input = read("input.txt")
const grid = map(stringSplit(stringJoin(stringSplit(input, "\n"), ""), ""), e => e == "@")
const int grid_size = ^/len(grid)

make stack = []

make c1 = 0

do {
    const x, y = i % grid_size, i / grid_size
    make at = grid#i
    if at then {
        const around, indexes #= count_around(x, y)
        if around < 4 then {
            set c1++
            do push(stack, i)
            do push(stack, j) for j in indexes
        }
    }
} for i in range(len(grid))

make c2 = 0

do {
    const i = pop(stack)
    const x, y = i % grid_size, i / grid_size
    make at = grid#i
    if at then {
        const around, indexes #= count_around(x, y)
        make removable = around < 4
        if removable then {
            set c2++
            set grid#i = false
            do push(stack, j) for j in indexes
        }
    }
} while len(stack) != 0

do sayln(`Part 1: {c1}`)
do sayln(`Part 2: {c2}`)

define count_around (x, y) {
    make count = 0
    make around_indexes = []
    do {
        do {
            continue when dx == 0 && dy == 0
            if is_in_bounds(x + dx, y + dy) then {
                if grid#((y + dy) * grid_size + (x + dx)) then {
                    set count++
                    do push(around_indexes, ((y + dy) * grid_size + (x + dx)))
                }
            }
        } for dx in [-1, 0, 1]
    } for dy in [-1, 0, 1]

    return [count, around_indexes]
}

define is_in_bounds (x, y) {
    return x >= 0 && x < grid_size && y >= 0 && y < grid_size
}