const input = read("input.txt")
const lines = stringSplit(input, "\n")

const locations = []
do push(locations, [i] + map(stringSplit(lines#i, ","), stringToInt)) for i in range(lines)

make circuits = {}
set circuits->(l#0) = [l#0] for l in locations

const pairs = []
do {
    const l1 = locations#i
    do {
        const l2 = locations#j
        do push(pairs, [l1#0, l2#0, (l1#1 - l2#1)**2 + (l1#2 - l2#2)**2 + (l1#3 - l2#3)**2])
    } for j in range(i + 1, len(locations))
} for i in range(len(locations))

do sort(pairs, (a, b) => a#2 - b#2)

make p1, p2 = 0

do {
    const p = pairs#pair_index
    make circuit1, circuit2 = -1

    if pair_index == 1000 then {
        const sizes = sort(map(values(circuits), v => len(v)))
        set p1 = sizes#(-1) * sizes#(-2) * sizes#(-3)
    }

    do {
        set circuit1 = c#0 when contains(circuits->(c#0), p#0)
        set circuit2 = c#0 when contains(circuits->(c#0), p#1)
    } for c in entries(circuits)

    if circuit1 != circuit2 then {
        set circuits->(circuit1) = removeDuplicates(circuits->(circuit1) + circuits->(circuit2))
        set circuits -= circuit2
    }

    if len(keys(circuits)) == 1 then {
        const location1 = locations#(p#0)
        const location2 = locations#(p#1)
        set p2 = location1#1 * location2#1
        break
    }
} for pair_index in range(len(pairs))

do sayln(`Part 1: {p1}`)
do sayln(`Part 2: {p2}`)